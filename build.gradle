buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		mavenCentral()
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
	}
}

plugins {
	id "org.ajoberstar.grgit" version "1.4.1"
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

import org.ajoberstar.grgit.Grgit

version = '2.0.0'
group = 'com.hea3ven.tools.commonutils'
archivesBaseName = 'h3nt-commonutils'

minecraft {
	version = '12.16.0.1782-1.9'
	mappings = "snapshot_20160321"
	runDir = "run"
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	mavenCentral()
}

dependencies {
	testCompile 'junit:junit:4.12'
}

task deobfJar(type: Jar) {
	from(sourceSets.main.output) {
		classifier = "deobf"
	}
}

artifacts {
	archives deobfJar
}

uploadArchives {
	// dependsOn 'reobf'
	repositories {
		mavenDeployer {
			repository(url: project.hasProperty('hea3venMvnUrl') ? project.hea3venMvnUrl : '')
		}
	}
}

def last_release_commit = '77bd604'
task updateChangelog << {
	def changelogFile = new File(projectDir, "CHANGELOG")
	def changelog = changelogFile.text

	def repo = Grgit.open(file("."))

	def additions = []
	def removes = []
	def fixes = []
	def extra = []
	repo.log { range(last_release_commit, 'HEAD') }.findAll { it.fullMessage.startsWith('* ') }.
			collect { it.fullMessage.trim().substring(2) }.each {
		if (it.startsWith('Add'))
			additions.add(it)
		else if (it.startsWith('Remove'))
			removes.add(it)
		else if (it.startsWith('Fix'))
			fixes.add(it)
		else
			extra.add(it)
	}

	changelog = '\n' + changelog
	(fixes + extra + removes + additions).each { changelog = '\t* ' + it + '\n' + changelog }

	changelog = version + ':\n' + changelog

	changelogFile.setText(changelog)
}
